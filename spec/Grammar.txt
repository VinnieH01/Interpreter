<program>         ::= (<top-level> ";")*

<top-level>       ::= "fn" IDENTIFIER "(" (IDENTIFIER ("," IDENTIFIER)*)? ")" "{" (<stmt> ";")* "}"
                    | <stmt>

<stmt>            ::= "{" (<stmt> ";")* "}"
                    | "print" <expr>
                    | "let" IDENTIFIER ":=" <expr>
                    | IDENTIFIER ":=" <expr>
                    | <if>
					| "while" "(" <expr> ")" <stmt>
                    | "ret" <expr>?
                    | <expr>

<if>              ::= "if" "(" <expr> ")" <stmt>
                    | "if" "(" <expr> ")" <stmt> "else" <stmt>

<expr>            ::= <logic>

<logic>           ::= <comparison> ("&&" | "||") <logic>
                    | <comparison>

<comparison>      ::= <product> (">" | "<" | "==" | ">=" | "<=") <sum>
                    | <sum>
					
<sum>             ::= <product> ("+" | "-") <sum>
                    | <product>
					
<product>         ::= <unary> ("*" | "/") <product>
                    | <unary>
					
<unary>           ::= "-" <unary>
                    | <primary>
					
<primary>         ::= LITERAL
                    | IDENTIFIER
                    | IDENTIFIER "(" (<expr> ("," <expr>)*)? ")"
                    | "input"
                    | "(" TYPE ")" <primary>
                    | "(" <expr> ")"

<comment>         ::= // ...
                    | /* ... */